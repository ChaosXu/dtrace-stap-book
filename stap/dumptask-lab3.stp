/**
 * taskdump.stp
 * 
 * Один раз в секунду печатает информацию по текущему процессу
 * Включает функции извлечения из task_struct
 */

/**
 * В старых ядрах структуры dentry и vfsmnt существовали раздельно.
 * В новых ядрах они были объединены в единую структуру path. 
 * 
 * SystemTap не кеширует полный путь до директории, поэтому 
 * нам бы пришлось воспользоваться функцией task_dentry_path(), чтобы
 * собрать его например так: 
 *      dentry = @cast(file, "file")->f_path->dentry;
 *      vfsmnt = @cast(file, "file")->f_path->mnt;
 *      return task_dentry_path(task, dentry, vfsmnt);
 * 
 * Однако в SystemTap присутствует баг 16991, закрытый в версии 2.6
 * Поэтому мы ограничимся выводом имени файла внутри директории
 */
function file_path:string(task:long, file:long) {
    if(@defined(@cast(file, "file")->f_vfsmnt))
        return d_name(@cast(file, "file")->f_dentry);    
    return d_name(@cast(file, "file")->f_path->dentry);  
}
function task_root_path:string(task:long, fs_ptr:long) {
    if(@defined(@cast(fs_ptr, "fs_struct")->rootmnt))
        return d_name(@cast(fs_ptr, "fs_struct")->root);
    return d_name(@cast(fs_ptr, "fs_struct")->root->dentry);
}
function task_pwd_path:string(task:long, fs_ptr:long) {
    if(@defined(@cast(fs_ptr, "fs_struct")->pwdmnt))
        return d_name(@cast(fs_ptr, "fs_struct")->pwd);
    return d_name(@cast(fs_ptr, "fs_struct")->pwd->dentry);    
}

/**
 * Печатает имя исполняемого файла по mm->exe_file */
function task_exefile(task:long, mm_ptr:long) {
	if(mm_ptr) {
		printf("\texe: %s\n", 
               file_path(task, @cast(mm_ptr, "mm_struct")->exe_file));
	}
}
/**
 * Печатает корневую и текущую директории процесса
 */
function task_paths(task:long, fs_ptr:long) {
	if(fs_ptr) {
		printf("\troot: %s\n", task_root_path(task, fs_ptr));
		printf("\tcwd: %s\n", task_pwd_path(task, fs_ptr));
	}
}

/**
 * Печатает аргументы. Аргументы отображены в виртуальное адресное 
 * пространство процесса и представляют собой область памяти (mm->arg_start; 
 * mm_arg_end), содержащую последовательно стоящие строки, например:
 * 
 * +-----+----+-------------+----+
 * | cat | \0 | /etc/passwd | \0 |
 * +-----+----+-------------+----+
 * ^                             ^
 * arg_start                     arg_end
 * 
 * ЗАМЕЧАНИЕ: функции user_string* читают из текущего пространства процесса. 
 * Для получения аргументов из стороннего процесса используйте Embedded C
 * и функцию ядра proc_pid_cmdline
 */
function task_args(mm_ptr:long) {
	if(mm_ptr) {
		arg_start = @cast(mm_ptr, "mm_struct")->arg_start;
		arg_end = @cast(mm_ptr, "mm_struct")->arg_end;
		if (arg_start != 0 && arg_end != 0)
		{
			len = arg_end - arg_start;
			nr = 0;

			/*Выбираем первый аргумент*/
			arg = user_string2(arg_start, "");
			while (len > 0)
			{
				printf("\targ%d: %s\n", nr, arg);
				arg_len = strlen(arg);
				arg_start += arg_len + 1;
				len -= arg_len + 1;
				nr++;

				arg = user_string2(arg_start, "");
			}
		}
	}
}

/**
 * Возвращает файловый дескриптор из таблицы по номеру fd
 * См. также: реализацию pfiles.stp
 */
function task_fd_filp:long(files:long, fd:long) {
	return @cast(files, "files_struct")->fdt->fd[fd];
}

function task_fds(task:long) {
	task_files = @cast(task, "task_struct", "kernel<linux/sched.h>")->files;

	if(task_files) {
		max_fds = task_max_file_handles(task);

		for (fd = 0; fd < max_fds; fd++) {
			filp = task_fd_filp(task_files, fd);
			if(filp) {
				printf("\tfile%d: %s\n", fd, file_path(task, filp));
			}
		}
	}
}

/**
 * Печатает разницу время запуска системы
 * start time - монотонное (monotonic)
 * real start time - с момента запуска системы (bootbased)
 * 
 * Замечание: эта функция работает с timespec, однако начиная с 3.17 они 
 * были заменены на u64
 */
function task_start_time_x(task:long) {
	if(@defined(@cast(task, "task_struct", "kernel<linux/sched.h>")
			->start_time)) {
		start_time_sec = @cast(task, "task_struct", "kernel<linux/sched.h>")
			->start_time->tv_sec;
		real_time_sec = @cast(task, "task_struct", "kernel<linux/sched.h>")
			->real_time->tv_sec;	
        printf("\tstart time: %ds\t real start time: %ds\n", start_time_sec, real_time_sec);
	}
	else {
      	real_time_sec = @cast(task, "task_struct", "kernel<linux/sched.h>")
			->real_start_time->tv_sec;
        printf("\treal start time: %ds\n", real_time_sec);
	}
	
}

/**
 * Печатает статистику выполнения задачи на процессоре */
function task_time_stats(task:long) {
	user = @cast(task, "task_struct", "kernel<linux/sched.h>")->utime;
	kernel = @cast(task, "task_struct", "kernel<linux/sched.h>")->stime;
	printf("\tuser: %s\t kernel: %s\n", cputime_to_string(user), cputime_to_string(kernel));
}

function dump_task(task:long) {
	task_mm = @cast(task, "task_struct", "kernel<linux/sched.h>")->mm;
	task_fs = @cast(task, "task_struct", "kernel<linux/sched.h>")->fs;

	printf("Task %p is %d@%d %s\n", task, task_pid(task), task_cpu(task), task_execname(task));

	task_exefile(task, task_mm);
	task_paths(task, task_fs);
	task_args(task_mm);
	task_fds(task);
	task_start_time_x(task);
	task_time_stats(task);
}

probe kprocess.exec_complete, kprocess.exit {
    dump_task(task_current());
}
